// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0 <0.9.0;

import "@openzeppelin/contracts-upgradeable/access/AccessControlUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";

import "../interfaces/IStrategy.sol";


abstract contract Strategy is IStrategy, Initializable, AccessControlUpgradeable, UUPSUpgradeable {
    bytes32 public constant UPGRADER_ROLE = keccak256("UPGRADER_ROLE");
    bytes32 public constant PORTFOLIO_MANAGER = keccak256("PORTFOLIO_MANAGER");

    address public portfolioManager;


    function __Strategy_init() internal initializer {
        __AccessControl_init();
        __UUPSUpgradeable_init();

        _grantRole(DEFAULT_ADMIN_ROLE, msg.sender);
        _grantRole(UPGRADER_ROLE, msg.sender);
    }

    function _authorizeUpgrade(address newImplementation)
    internal
    onlyRole(UPGRADER_ROLE)
    override
    {}

    // ---  modifiers

    modifier onlyPortfolioManager() {
        require(hasRole(PORTFOLIO_MANAGER, msg.sender), "Restricted to PORTFOLIO_MANAGER");
        _;
    }

    modifier onlyAdmin() {
        require(hasRole(DEFAULT_ADMIN_ROLE, msg.sender), "Restricted to admins");
        _;
    }

    // --- setters

    function setPortfolioManager(address _value) public onlyAdmin {
        require(_value != address(0), "Zero address not allowed");

        revokeRole(PORTFOLIO_MANAGER, portfolioManager);
        grantRole(PORTFOLIO_MANAGER, _value);

        portfolioManager = _value;
        emit PortfolioManagerUpdated(_value);
    }


    // --- logic


    function stake(
        address _asset, // USDC
        uint256 _amount // value for staking in USDC
    ) external override onlyPortfolioManager {
        emit Stake(_amount);
        _stake(_asset, IERC20(_asset).balanceOf(address(this)));
    }

    function unstake(
        address _asset,
        uint256 _amount,
        address _beneficiary,
        bool _targetIsZero
    ) external override onlyPortfolioManager returns (uint256) {
        uint256 withdrawAmount;
        if (_targetIsZero) {
            withdrawAmount = _unstakeFull(_asset, _beneficiary);
        } else {
            withdrawAmount = _unstake(_asset, _amount, _beneficiary);
            require(withdrawAmount >= _amount, 'Returned value less than requested amount');
        }

        uint256 balanceUSDC = IERC20(_asset).balanceOf(address(this));
        IERC20(_asset).transfer(_beneficiary, balanceUSDC);

        emit Unstake(_amount, balanceUSDC);

        return balanceUSDC;
    }

    function claimRewards(address _to) external override onlyPortfolioManager returns (uint256) {
        uint256 totalUsdc = _claimRewards(_to);
        emit Reward(totalUsdc);
        return totalUsdc;
    }

    function _stake(
        address _asset,
        uint256 _amount
    ) internal virtual {
        revert("Not implemented");
    }

    function _unstake(
        address _asset,
        uint256 _amount,
        address _beneficiary
    ) internal virtual returns (uint256){
        revert("Not implemented");
    }

    function _unstakeFull(
        address _asset,
        address _beneficiary
    ) internal virtual returns (uint256){
        revert("Not implemented");
    }

    function _claimRewards(address _to) internal virtual returns (uint256){
        revert("Not implemented");
    }

    function _logExchangeStart(address[] memory tokens) internal returns (SwapInfo memory){
        SwapInfo memory swapInfo = SwapInfo({
        tokens : tokens,
        balancesBefore : uint256[](tokens.length),
        balancesAfter : uint256[](tokens.length)
        });


        for (uint8 i; i < tokens.length; i++) {

            address token = tokens[i];
            swapInfo.balancesBefore[i] = IERC20(token).balanceOf(address(this));
        }

        return swapInfo;
    }

    function _logExchangeEnd(SwapInfo memory swapInfo, address exchange, string memory code) internal {

        for (uint8 i; i < swapInfo.tokens.length; i++) {

            address token = swapInfo.tokens[i];
            swapInfo.balancesAfter[i] = IERC20(token).balanceOf(address(this));
        }

        emit Exchange(exchange, code, swapInfo);

    }


    uint256[49] private __gap;
}
